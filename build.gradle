buildscript {
    repositories {
        jcenter()

        maven { url = 'https://repo.spongepowered.org/maven' }
        maven { url = 'http://files.minecraftforge.net/maven' }
    }

    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
        classpath 'org.spongepowered:mixingradle:0.6-SNAPSHOT'
    }
}

apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'org.spongepowered.mixin'
apply plugin: 'maven-publish'

group = "me.jellysquid.mods"

if (System.getenv("BUILD_NUMBER") != null) {
    version = "${minecraft_version}-${mod_version}+build${System.getenv("BUILD_NUMBER")}"
} else {
    version = "${minecraft_version}-${mod_version}-SNAPSHOT"
}

compileJava {
    sourceCompatibility = targetCompatibility = '1.8'
}

minecraft {
    version = "${minecraft_version}-${forge_version}"
    runDir = "run"

    mappings = forge_mappings

    clientJvmArgs += "-Dfml.coreMods.load=me.jellysquid.mods.phosphor.core.PhosphorFMLLoadingPlugin"
    serverJvmArgs += "-Dfml.coreMods.load=me.jellysquid.mods.phosphor.core.PhosphorFMLLoadingPlugin"

    makeObfSourceJar = false
}

repositories {
    maven { url = 'https://repo.spongepowered.org/maven' }
}

configurations {
    shade
    compile.extendsFrom shade
}

dependencies {
    shade('org.spongepowered:mixin:0.7.11-SNAPSHOT') {
        transitive = false
    }
}

def commonManifest = {
    attributes "FMLAT": "phosphor_at.cfg"
    attributes "FMLCorePlugin": "me.jellysquid.mods.phosphor.core.PhosphorFMLLoadingPlugin"
    attributes "TweakClass": "org.spongepowered.asm.launch.MixinTweaker"
    attributes "TweakOrder": 0
    attributes "FMLCorePluginContainsFMLMod": true
    attributes "ForceLoadAsMod": true
    attributes "Maven-Artifact": "${group}:${name}:${version}"
}

jar {
    classifier = 'universal'

    manifest commonManifest

    from sourceSets.main.output

    configurations.shade.each { dep ->
        from(project.zipTree(dep)) {
            rename 'LICENSE.txt', 'org/spongepowered/LICENSE.txt'

            exclude 'META-INF/*.SF'
            exclude 'META-INF/*.RSA'
            exclude 'META-INF/*.MF'

            exclude '**/*.html'
        }
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'

    manifest commonManifest

    from sourceSets.main.allSource
}

task deobfJar(type: Jar, dependsOn: reobfJar) {
    classifier = 'dev'

    manifest commonManifest

    from sourceSets.main.output
}

import net.minecraftforge.gradle.user.TaskExtractAnnotations

task deobfJarExtractAnnotations(type: TaskExtractAnnotations, dependsOn: deobfJar) {
    jar = deobfJar.archivePath
}

deobfJar.finalizedBy deobfJarExtractAnnotations

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'

        expand 'version': project.version, 'mcversion': project.minecraft.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

mixin {
    add sourceSets.main, "mixins.phosphor.refmap.json"
}

publishing {
    publications {
        mod(MavenPublication) {
            artifact jar
            artifact deobfJar
            artifact sourcesJar
        }
    }

    repositories {
        def mavenSecretsPath = System.getenv("MAVEN_SECRETS_FILE")

        if (mavenSecretsPath != null) {
            def mavenSecretsFile = file(mavenSecretsPath).newReader()
            def mavenSecrets = new Properties()
            mavenSecrets.load(mavenSecretsFile)

            maven {
                url = mavenSecrets.publishUrl

                credentials {
                    username = mavenSecrets.username
                    password = mavenSecrets.password
                }
            }
        }
    }
}

if (System.getenv("JARSIGN_SECRETS_FILE") != null) {
    def secretsFile = file(System.getenv("JARSIGN_SECRETS_FILE"))

    def secrets = new Properties()
    secrets.load(secretsFile.newReader())

    [jar, sourcesJar, deobfJar].each { jarTask ->
        def task = task(jarTask.name + 'Sign', type: SignJar) {
            keyStore = System.getenv("JARSIGN_KEYSTORE_FILE")
            alias = secrets.alias
            storePass = secrets.storePassword
            keyPass = secrets.keyPassword

            inputFile = jarTask.archivePath
            outputFile = jarTask.archivePath
        }

        jarTask.finalizedBy task
    }
}
